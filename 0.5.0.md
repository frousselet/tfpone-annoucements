# What's new in Terraform Provider for OpenNebula 0.5.0?

Today, at [Iguane Solutions](https://www.ig1.com) we are glad to introduce you the new release of the Terraform Provider for OpenNebula.

## Contributors

- [Pierre Lafièvre](https://github.com/treywelsh) (Iguane Solutions)
- [François Rousselet](https://github.com/frousselet) (Iguane Solutions)

## New features and changes

### Terraform SDK v2

Starting version 0.5.0, the Terraform Provider for OpenNebula uses the second version of the Terraform SDK.

It includes a number of new features like _Diagnostics_, improved acceptance tests, support for _Resource-Level_ and _Field-Level_ descriptions and much more.

Please note the support of Terraform 0.11 is now dropped and the Provider is compatible with Terraform 0.12 and higher. For more information, please refer to Terraform SDK [changelog](https://github.com/hashicorp/terraform-plugin-sdk/blob/v2.0.0/CHANGELOG.md).

In the next releases, we will continue to include Terraform SDK v2 new features.

### Add support for Virtual Routers

The Terraform Provider now supports Virtual Routers. We implemented the following new resources:

- `opennebula_virtual_router_instance_template`
- `opennebula_virtual_router_instance`
- `opennebula_virtual_router_nic`
- `opennebula_virtual_router`

```hcl
# Create a new virtual router instance template

resource "opennebula_virtual_router_instance_template" "example" {
  name        = "virtual-router-instance-template"
  permissions = "642"
  group       = "oneadmin"
  cpu         = "0.5"
  vcpu        = "1"
  memory      = "512"

  context = {
    dns_hostname = "yes"
    network      = "YES"
  }

  graphics {
    keymap = "en-us"
    listen = "0.0.0.0"
    type   = "VNC"
  }

  os {
    arch = "x86_64"
    boot = ""
  }

  tags = {
    env = "prod"
  }
}

# Create a new virtual router

resource "opennebula_virtual_router" "example" {
  name        = "virtual-router"
  permissions = "642"
  group       = "oneadmin"
  description = "This is an example of virtual router"

  instance_template_id = opennebula_virtual_router_instance_template.example.id

  lock = "USE"

  tags = {
    environment = "example"
  }
}

# Instanciate the virtual router

resource "opennebula_virtual_router_instance" "example" {
  name        = "virtual-router-instance"
  group       = "oneadmin"
  permissions = "642"
  memory      = 128
  cpu         = 0.1

  virtual_router_id = opennebula_virtual_router.example.id

  tags = {
    environment = "example"
  }
}

# Create a new network

resource "opennebula_virtual_network" "example" {
  name   = "virtual-network"
  type   = "bridge"
  bridge = "onebr"
  mtu    = 1500

  ar {
    ar_type = "IP4"
    size    = 12
    ip4     = "172.16.100.130"
  }

  permissions     = "642"
  group           = "oneadmin"
  security_groups = [0]
  clusters        = [0]
}

# Attach the virtual router to the network

resource "opennebula_virtual_router_nic" "example" {
  virtual_router_id = opennebula_virtual_router.example.id
  network_id        = opennebula_virtual_network.example.id
}
```

### Add resource `opennebula_group_admins`

In this version, we added a new resource to attach admin users to groups:

```hcl
data "template_file" "example" {
  template = file("group_template.txt")
}

# Create a new group

resource "opennebula_group" "example" {
  name     = "group"
  template = data.template_file.example.rendered

  lifecycle {
    ignore_changes = [
      admins               # Ignore deprecated admins attribute
    ]
  }
}

# Create a new user

resource "opennebula_user" "example" {
  name          = "John"
  password      = "p@ssw0rd"
  auth_driver   = "core"
  primary_group = opennebula_group.example.id
}

# Set the user as a group admin

resource "opennebula_group_admins" "example" {
  group_id = opennebula_group.example.id
  users_ids = [
    opennebula_user.example.id
  ]
}
```

### Improve `opennebula_group` Sunstone template

In this release, we also changed how we work with `opennebula_group` template.

Starting 0.5.0, we introduced a new `sunstone` attribute:

```hcl
resource "opennebula_group" "group" {
  name = "group"

  sunstone = {
      default_view             = "cloud",
      group_admin_default_view = "groupadmin",
      group_admin_views        = "cloud",
      views                    = "cloud"
  }

  lifecycle {
    ignore_changes = [
      template             # Ignore deprecated template attribute
    ]
  }
}
```

## What's next?

- [Add support for public cloud](https://github.com/OpenNebula/terraform-provider-opennebula/issues/186)
- [Add support for IPv6](https://github.com/OpenNebula/terraform-provider-opennebula/issues/247)
- [Add new resource `opennebula_address_range`](https://github.com/OpenNebula/terraform-provider-opennebula/issues/279)

For more details, please refer to the [GitHub repository](https://github.com/OpenNebula/terraform-provider-opennebula) and the [release notes](https://github.com/OpenNebula/terraform-provider-opennebula/releases/tag/v0.5.0). Your issues and pull requests are welcome!
